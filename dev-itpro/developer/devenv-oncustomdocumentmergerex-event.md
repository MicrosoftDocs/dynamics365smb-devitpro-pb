---
title: "OnCustomDocumentMergerEx Event"
description: Describe the OnCustomDocumentMergerEx Event in Business Central.
ms.custom: na
ms.date: 01/26/2022
ms.reviewer: solsen
ms.suite: na
ms.tgt_pltfrm: na
ms.topic: conceptual
ms.service: "dynamics365-business-central"
author: jswymer
---

# OnCustomDocumentMergerEx Event

This article describes the syntax of the OnCustomDocumentMergerEx event which will enable use of custom renders given a dataset and a layout. The layout must be specified as a Custom Layout in the rendering section within the report definition.

## Usage

Use the OnCustomDocumentMergerEx event to specify what happens when the user has specified a custom report layout type that is to be rendered into an artifact in application code. For more information about subscribing to this event, see [Developing Report Custom Render Extensions Overview](devenv-report-custom-render.md).

## Publisher

Codeunit **44 ReportManagement**.

## Raised

When a user runs a report that uses a custom layout format handled by application code (extension).

## Syntax

```AL
 [IntegrationEvent(false, false)]
local procedure OnCustomDocumentMergerEx(ObjectID: Integer; ReportAction: Option SaveAsPdf,SaveAsWord,SaveAsExcel,Preview,Print,SaveAsHtml; ObjectPayload: JsonObject; XmlData: InStream; LayoutData: InStream; var DocumentStream: OutStream; var IsHandled: Boolean)
```

## Parameters

### *ObjectID*

Type: [Integer](methods-auto/integer/integer-data-type.md)

The ID of the report object to be run.

### *ReportAction*

Type: [Option]

Specifies the action to take in the custom document merge process.

Valid values include:

- `SaveAsPdf` Generates a PDF document in the target stream.
- `SaveAsWord` Generates a Microsoft Word document in the target stream.
- `SaveAsExcel` Generates a Microsoft Excel document in the target stream.
- `SaveAsHtml` Generates an HTML document in the target stream.
- `Preview` Generates a PDF document in the target stream, which the platform will download to the client for preview.
- `Print` Generates a PDF document in the target stream, which the platform will print using the printer specified for the present report ID.

### *ObjectPayload*

Type: [JsonObject](methods-auto/jsonobject/jsonobject-data-type.md)

Instance of the report payload. For more information, see [Report payload structure](#reportpayload).

### *XmlData*

Type: [InStream](methods-auto/instream/instream-data-type.md)

A stream object that contains the XML dataset generated by the platform runtime. This stream will contain the input data for the rendering process.

### *LayoutData*

Type: [InStream](methods-auto/instream/instream-data-type.md)

A stream object that contains the report layout to be used in the rendering process. The platform provides this data from the standard report layout selection logic and it will contain the currently selected layout.

### *Success*

Type: [Boolean](methods-auto/boolean/boolean-data-type.md)

Specifies whether the extension handled the merge action successfully.

> [!NOTE]  
> The platform will handle further processing of the result stream and the extension code must return the rendered artifact in the target stream. The platform will handle:
>
> - Saving to a specified file in save-as scenarios
> - Saving to a given stream
> - Download to the client
> - Print or preview

## Sample Code

The simplest possible custom document render can can be implemented like the following sample that will use the existing application logic to render Xml datasets into Microsoft Word or Pdf documents using a given template (Word template).

```al
    [EventSubscriber(ObjectType::Codeunit, Codeunit::ReportManagement, 'OnCustomDocumentMergerEx', '', true, true)]
    local procedure OnCustomDocumentMergerEx(ObjectID: Integer; ReportAction: Option SaveAsPdf,SaveAsWord,SaveAsExcel,Preview,Print,SaveAsHtml; ObjectPayload: JsonObject; XmlData: InStream; LayoutData: InStream; var DocumentStream: OutStream; var IsHandled: Boolean)
    var
        DocumentReportMgt: Codeunit "Document Report Mgt.";
        TempBlob: Codeunit "Temp Blob";
        DataInStream: InStream;
        DataOutStream: OutStream;
        ObjectName: JsonToken;
        DocumentTypeParts: List of [Text];
        DocumentType: JsonToken;
        Extension: Text;
        Token: JsonToken;
        MimeType: Text;
        LayoutName: Text;
        LayoutModel: Text;
        JsonText: Text;
        JsonFile: File;
    begin        
        if IsHandled then
            exit;

        Init();

        // Sample code to persist the json object to disk
        ObjectPayload.WriteTo(JsonText);
        JsonFile.TextMode := true;
        JsonFile.Create(TempFolderPath + 'OnCustomDocumentMergerEx.json', TextEncoding::UTF8);
        JsonFile.Write(JsonText);
        JsonFile.Close();

        // Get report options from the json object
        ObjectPayload.Get('objectname', ObjectName);
        ObjectPayload.Get('documenttype', DocumentType);
        ObjectPayload.Get('layoutmimetype', Token);
        MimeType := Token.AsValue().AsText();

        ObjectPayload.Get('layoutname', Token);
        LayoutName := Token.AsValue().AsText();

        ObjectPayload.Get('layoutmodel', Token);
        LayoutModel := Token.AsValue().AsText();

        DocumentTypeParts := DocumentType.AsValue().AsText().Split('/');
        
        // Notice that the Extension string below have to be remapped to a standard file extension
        // based on the document mimetype.
        Extension := DocumentTypeParts.Get(DocumentTypeParts.Count);

        TempBlob.CreateOutStream(DataOutStream);

        // The 
        case ReportAction of
            ReportAction::SaveAsPdf, ReportAction::Preview, ReportAction::Print:
                begin
                    if not DocumentReportMgt.TryXmlMergeWordDocument(LayoutData, XmlData, DataOutStream) then
                        error('Unable to handle custom document merge');
                    DocumentReportMgt.ConvertWordToPdf(TempBlob, ObjectID);
                    TempBlob.CreateInStream(DataInStream);
                    CopyStream(DocumentStream, DataInStream);
                    IsHandled := true;
                end;
            ReportAction::SaveAsHtml:
                begin
                    if not DocumentReportMgt.TryXmlMergeWordDocument(LayoutData, XmlData, DataOutStream) then
                        error('Unable to handle custom document merge');
                    DocumentReportMgt.ConvertWordToHtml(TempBlob);
                    TempBlob.CreateInStream(DataInStream);
                    CopyStream(DocumentStream, DataInStream);
                    IsHandled := true;
                end;
            ReportAction::SaveAsWord:
                begin
                    if not DocumentReportMgt.TryXmlMergeWordDocument(LayoutData, XmlData, DocumentStream) then
                        error('Unable to handle custom document merge');
                    IsHandled := true;
                end;
            else
                error('Unsupported report action %0', ReportAction);
        end;
    end;
```

## <a name="reportpayload"></a>Report payload

The report payload provides information about the current report instamce and selected layout. The payload consists of several attributes and has the following structure.

```json
{
    "filterviews":
    [
        {"name":"Header","tableid":112,"view":"VERSION(1) SORTING(Field3) WHERE(Field3=1(103027))"},
        {"name":"Line","tableid":113,"view":"VERSION(1) SORTING(Field3,Field4) WHERE(Field4=1(0..10000))"},
        {"name":"ShipmentLine","tableid":7190,"view":"VERSION(1) SORTING(Field1,Field2,Field3) WHERE(Field2=1(10000))"}
    ],
    "version":1,
    "objectname":"Standard Sales - Invoice",
    "objectid":1306,
    "documenttype":"application/pdf",
    "invokedby":"00000000-0000-0000-0000-000000000001",
    "invokeddatetime":"2020-01-17T15:33:52.48+01:00",
    "companyname":"CRONUS International Ltd.",
    "printername":"My Printer",
    "duplex":false,
    "color":false,
    "defaultcopies":1,
    "papertray": null,
    "layoutmodel": "Custom",
    "layoutname": "CustomLayout",
    "layoutmimetype": "Application/ReportLayout/Custom",
}

```

### Attributes

*objectname*

Specifies the name of the object.

*objectid*

Specifies the ID of the object.

*documenttype*

Specifies the MIME type of the document. Currently only `application/pdf` is supported. 

*documeninvokedby*

Specifies the ID of the user who invoked the print action.  

*invokeddatetime*

Specifies the date and time that the print action was invoked, for example, 2019-10-22T22:25:54.338+02:00. The value is the date and time on the client machine.  

*companyname*

Specifies the Business Central company.

*version*

Specifies the version of the printer setup in the payload. Currently, the only supported version for the payload is `1`.

*duplex*

Specifies whether to use duplex (2-sided) printing. `true` specifies duplex printing; otherwise `false`.

> [!NOTE]
>  `duplex` is currently not used and ignored at runtime.

*color*

Specifies whether color printing is set.  `true` specifies color printing; otherwise `false`. 

> [!NOTE]
> `color` is currently not used and ignored at runtime.

*defaultcopies*

Specifies the number of copies to print by default. The default is `1`.

> [!NOTE]
>  `defaultcopies` is currently not used and ignored at runtime.

*filterviews*

Specifies views that are used on the document.

*name*

Specifies the name of data item that the filter view applies to.

*tableid*

Specifies ID of the table for the view.

*view*

Specifies the name of the view.

*intent*

*layoutmodel*
Specifies the report render type to use (Rdlc, Word, Excel or Custom)

*layoutname*

Specifies the name of the layout.

*layoutmimetype*

Specifies a mime type that identifies the custom layout type.

## See Also

[Working With and Troubleshooting Payloads](devenv-reports-troubleshoot-printing.md)
[Developing Report Custom Render Extensions Overview](devenv-report-custom-render.md)
[Developing Printer Extensions Overview](devenv-reports-printing.md)
[Creating a Printer Extension](devenv-reports-create-printer-extension.md)
[Events in AL](devenv-events-in-al.md)
[Publishing Events](devenv-publishing-events.md)
[Raising Events](devenv-raising-events.md)
[Subscribing to Events](devenv-subscribing-to-events.md)
